
      
      FUNCTION WLOOSE(NF,EPS,X,E)
      FUNCTION FAME(KZR1,E,JOT,SSTAR,AE,BE)
      FUNCTION FANHRO(KANH,E,NVIBR,NYR,DR)
      FUNCTION FANHWW(KANH,EI,NVIB1,NVIB2,NY1,NY2,D1,D2)
      FUNCTION BCENT(JOT)
      FUNCTION F(Z)
      FUNCTION FDERIV(Z)
      FUNCTION RRHIND(S,V0,E)
      FUNCTION RWHIND(S,V0,E)
      FUNCTION DGAMMA(X)
      FUNCTION GAMMLN(XX)
      FUNCTION FQHIND(KT,V0,QFREE,N)
      FUNCTION WLLIAT (E,J,B)
      FUNCTION WLSTAT(E,J,B)
      FUNCTION WLLILI(E,JG,B1,B2)
      FUNCTION WLLOLL(E,B1,B2)
      FUNCTION WLHILL(E,B1,B2)
      FUNCTION WLSTLI(E,JG,BST,BLI)
      FUNCTION WLLOSL(E,BST,BLI)
      FUNCTION WLHISL(E,BST,BLI)
      FUNCTION WLSTST(E,JG,BST1,BST2)
      FUNCTION WLLOSS(E,BST1,BST2)
      FUNCTION WLHISS(E,BST1,BST2)
      FUNCTION POLAT(X)
      FUNCTION QSTAR(EPS,X,KT)
      FUNCTION AWR(E,BETA)



C********************************************************************
      FUNCTION WLOOSE(NF,EPS,X,E)
C********************************************************************
C This function calculates the integrated density of states of the
C disappearing oscillators and conserved internal rotors according to
C eq. (3.3) in JCP 86, 6171 (1987).
C********************************************************************
      COMMON/SPOLL/NFG,EPSE(110),EPSP(110),XE(110),XP(110),
     *             EPSTS(110),XTS(110)
      DOUBLE PRECISION DGAMMA
      DIMENSION EPS(110),X(110)
      GH=1.772454
      WLOOSE=1.
      R=0.
      SUMGAM=1.
      PROGAM=1.
      DO 1 I=1,NF
      IF(XP(I).GT.0.51) GOTO 1
      IF(XE(I).LT.0.51.AND.XP(I).LT.0.51) THEN
          R=R+1.
          SUMGAM=SUMGAM+.5
          WLOOSE=WLOOSE*SQRT(E/EPS(I)+.5)
      ELSE
          PROGAM=PROGAM*DGAMMA(DBLE(1.+X(I)))
          SUMGAM=SUMGAM+X(I)
          WLOOSE=WLOOSE*(E/EPS(I)+.5)**X(I)
      ENDIF
      
      WLOOSE=.5*GH**R*PROGAM/DGAMMA(DBLE(SUMGAM))*WLOOSE
C The factor .5 corrects for the fact that the K-rotor is formally
C taken in the subroutine as free internal rotor but has to be taken
C as a disappearing degree of freedom.
C
      RETURN
      END
      
      
C********************************************************************      
      FUNCTION FAME(KZR1,E,JOT,SSTAR,AE,BE)
C**********************************************************************
C This function calculates angular momentum coupling factors Fame(E,J).
C Ref.: JCP 79, 6017 (1983), eqs. A17, C1, C11, prolate symmetric top
C expression (C10) modified after Ber. Bunsenges. Phys. Chem. 98, 1563
C (1994).
C**********************************************************************
      DOUBLE PRECISION DGAMMA,QGAM
      QGAM=DGAMMA(DBLE(SSTAR))*DGAMMA(DBLE(1.5))/DGAMMA(DBLE(SSTAR+.5))
      ARG=AE/E
      ARGP=AE/(AE-BE)
      GOTO(3,4,4,5),KZR1
    3 SSTAR=SSTAR+1.
C Linear molecule, SSTAR one to small, because EPSE(NFG) is not the
C K-rotor, see main program.
      F1=DGAMMA(DBLE(SSTAR+1.))/DGAMMA(DBLE(SSTAR-1.))
     *   *(1+JOT)*(1.+JOT/2.)*ARG*ARG
      FAME=F1/(F1+1.)
      RETURN
    4 FAME=(2.*JOT+1.)*SQRT(ARG)/QGAM
      RETURN
    5 IF(JOT.EQ.0) THEN
          FAME=SQRT(ARG)/(QGAM)
          GOTO 6
      END IF
      X=(AE-BE)*(JOT+1)*(JOT+1)/E
      FAME=SQRT(ARGP)
     *     *TANH(1./(2.*QGAM)*(1.+(2.+SSTAR)/10.*X)*SQRT(X)*2.)
    6 RETURN
      END

      
      
      
C********************************************************************      
      FUNCTION FANHRO(KANH,E,NVIBR,NYR,DR)
C********************************************************************      
C This function calculates anharmonicity factors for the densities of     
C states of the reactant molecule.
C********************************************************************      
      REAL NYR(100),DR(100)

      GOTO (2,3), KANH+1

    3 NENNER=2*NVIBR-3
      FANHRO=1.
      DO 1 I=1,NVIBR
    1 FANHRO=FANHRO*(1.+(E+NYR(I)/2.)/DR(I)/NENNER)

      RETURN
    2 FANHRO=1.
      RETURN
      END
      
      
C********************************************************************      
      FUNCTION FANHWW(KANH,EI,NVIB1,NVIB2,NY1,NY2,D1,D2)
C********************************************************************
C This function calculates anharmonicity factors for the sums of
C states of the product molecules.
C********************************************************************
      REAL NY1(100),NY2(100),D1(100),D2(100)
      GOTO(2,3),KANH+1
    3 NENNER=2*(NVIB1+NVIB2)-1
      FANHWW=1.
      DO 1 I=1,NVIB1
    1 FANHWW=FANHWW*(1.+(EI+NY1(I)/2.)/D1(I)/NENNER)
      DO 4 I=1,NVIB2
    4 FANHWW=FANHWW*(1.+(EI+NY2(I)/2.)/D2(I)/NENNER)
      RETURN
    2 FANHWW=1.
      RETURN
      END
      
C********************************************************************      
      FUNCTION BCENT(JOT)
C**********************************************************************
C This function calculates for a given J the minimum of the lowest
C channel potential, estimating in this way the J-dependence of the 
C equilibrium value for Bcent. The minimum is determined by a Newton-
C iteration of the first derivative. 
C *********************************************************************
      COMMON/BCEN/BE,DMORSE,A1,A2
      COMMON/CENT/ ECENTD
      IF(JOT)1002,1001,1002
 1001 BCENT=BE
      RETURN
 1002 ECENTD=BE*JOT*(JOT+1)/DMORSE
      ZNEW=1E-10
 1003 ZOLD=ZNEW
      ZNEW=ZOLD-F(ZOLD)/FDERIV(ZOLD)
      TOL=ABS(ZNEW-ZOLD)
      IF(TOL.LE.1E-4) GOTO 1005
      GOTO 1003
 1005 BCENT=BE/(1.+A1*ZNEW+A2*ZNEW*ZNEW)
      RETURN
      END
      
C********************************************************************      
      FUNCTION F(Z)
C********************************************************************
C First derivative of the lowest channel potential.
C********************************************************************
      COMMON/BCEN/BE,DMORSE,A1,A2
      COMMON/CENT/ ECENTD
      BRACKT=1.+A1*Z+A2*Z*Z
      F=2.*(1.-EXP(-Z))*EXP(-Z)-ECENTD*(A1+2.*A2*Z)/(BRACKT*BRACKT)
      RETURN
      END
      
C********************************************************************      
      FUNCTION FDERIV(Z)
C********************************************************************
C Second derivative of the lowest channel potential.
C********************************************************************
      COMMON/BCEN/BE,DMORSE,A1,A2
      COMMON/CENT/ ECENTD
      BRACKT=1.+A1*Z+A2*Z*Z
      FACTOR=((A1+2.*A2*Z)**2-A2*BRACKT)/(BRACKT*BRACKT*BRACKT)
      FDERIV=4.*EXP(-2.*Z)-2.*EXP(-Z)+2.*ECENTD*FACTOR
      RETURN
      END
  
C********************************************************************      
      FUNCTION RRHIND(S,V0,E)
C*********************************************************************
C This function calculates the correction factor rhohind(E)/rhofree(E)
C for the densities of states of an ensemble of S oscillators and
C one hindered rotor with an hindrance potential V0.
C*********************************************************************
      INTEGER S
      DOUBLE PRECISION PI,VE,A,GS,B(0:100),RHIND,DGAMMA
      SAVE IC,PI,GS,A,B
      DATA IC,PI /0,1.772453850905516/
      IF(V0.GT.0..AND.E.GT.0.) GOTO 5
      RRHIND=1.
      RETURN
    5 IC=IC+1
      IF(IC.GT.1) GOTO 2
      GS=DGAMMA(DBLE(S+.5))
      A=GS/(PI*DGAMMA(DBLE(S+1.)))
      DO 1 NY=0,S-2
    1 B(NY)=(-1)**NY*GS*(NY+2.5)/(DGAMMA(DBLE(NY+1.))
     *    *DGAMMA(DBLE(S-1.-NY))*PI*(NY+2.)*(NY+1.5))
    2 VE=DBLE(V0)/DBLE(E)
      IF(VE.LT.1.) GOTO 3
      RHIND=A/DSQRT(VE)
      RRHIND=REAL(RHIND)
      RETURN
    3 RHIND=1.
      DO 4 I=0,S-2
      RHIND=RHIND-B(I)*VE**(I+1.5)
    4 RRHIND=REAL(RHIND)
      RETURN
      END
      
C********************************************************************      
      FUNCTION RWHIND(S,V0,E)
C*********************************************************************
C This function calculates the correction factor Whind(E)/Wfree(E)
C for the sums of states of an ensemble of S oscillators and
C one hindered rotor with an hindrance potential V0.
C*********************************************************************
      INTEGER S
      DOUBLE PRECISION PI,VE,A,GS,B(0:100),WHIND,DGAMMA
      SAVE IC,PI,GS,A,B
      DATA IC,PI /0,1.772453850905516/
      IF(V0.GT.0..AND.E.GT.0.) GOTO 5
      RWHIND=1.
      RETURN
    5 IC=IC+1
      IF(IC.GT.1) GOTO 2
      GS=DGAMMA(DBLE(S+1.5))
      A=GS/(PI*DGAMMA(DBLE(S+2.)))
      DO 1 NY=0,S-1
    1 B(NY)=(-1)**NY*GS*(NY+2.5)/(DGAMMA(DBLE(NY+1.))
     *    *DGAMMA(DBLE(S-NY))*PI*(NY+2.)*(NY+1.5))
    2 VE=DBLE(V0)/DBLE(E)
      IF(VE.LT.1.) GOTO 3
      WHIND=A/DSQRT(VE)
      RWHIND=REAL(WHIND)
      RETURN
    3 WHIND=1.
      DO 4 I=0,S-1
      WHIND=WHIND-B(I)*VE**(I+1.5)
    4 RWHIND=REAL(WHIND)
      RETURN
      END
      
C********************************************************************      
      FUNCTION DGAMMA(X)
C********************************************************************
C Calculation of the Gamma-function with double precision.
C******************************************************************** 
      DOUBLE PRECISION PI,DGAMMA,Z,GAMMLN,X
      DATA PI /3.141592654/
      IF (X-1.) 1,2,2
    1 Z=1.-X
      DGAMMA=PI*Z/(DEXP(GAMMLN(1.+Z))*SIN(PI*Z))
      RETURN
    2 DGAMMA=DEXP(GAMMLN(X))
      RETURN
      END
      
C********************************************************************      
      FUNCTION GAMMLN(XX)
C********************************************************************
C Calculation of ln(gamma(x)), following W. H. Press et al.,
C 'Numerical Recipes', Cambridge Univ. Press 1989
C********************************************************************
      DOUBLE PRECISION COF(6),STP,HALF,ONE,FPF,X,TMP,SER,XX,
     *GAMMLN
      DATA COF,STP/76.18009173D0,-86.50532033D0,24.01409822D0,
     *-1.231739516D0,.120858003D-2,-.536382D-5,2.50662827465D0/
      DATA HALF,ONE,FPF/0.5D0,1.0D0,5.5D0/
      X=XX-ONE
      TMP=X+FPF
      TMP=(X+HALF)*LOG(TMP)-TMP
      SER=ONE
      DO 11 J=1,6
      X=X+ONE
   11 SER=SER+COF(J)/X
      GAMMLN=TMP+LOG(STP*SER)
      RETURN
      END
      
C********************************************************************      
      FUNCTION FQHIND(KT,V0,QFREE,N)
C********************************************************************
C This routine calculates the correction factor between the partition
C function of a hindered and a free internal rotor 
C********************************************************************
      REAL KT,KTV0
      IF(V0)1,2,1
    1 KTV0=KT/V0
      HNYKT=N*SQRT(3.1416/KTV0)/QFREE
      FQHIND=EXP(-1.2*KTV0)/(QFREE*(1.-EXP(-HNYKT)))
     *    +(1.-EXP(-KTV0))**1.2
      RETURN
    2 FQHIND=1.
      RETURN
      END
      
C********************************************************************      
      FUNCTION WLLIAT (E,J,B)
C*********************************************************************
C This function calculates exact values of Wl(E,J) for systems linear/
C atom.
C*********************************************************************
      JMAX=INT(SQRT(.25+E/B)-.5)
      IF(J.GT.JMAX) GOTO 1
      WLLIAT=REAL((2*J+1)*(JMAX+1)-J*(J+1))
      RETURN
    1 WLLIAT=REAL((JMAX+1)*(JMAX+1))
      RETURN
      END
      
C********************************************************************      
      FUNCTION WLSTAT(E,J,B)
C********************************************************************
C This function calculates exact values of Wl(E,J) for systems
C spherical top/atom.
C********************************************************************
      JMAX=INT(SQRT(.25+E/B)-.5)
      IF(J.GT.JMAX) GOTO 1
      WLSTAT=REAL((2*J+1)*(JMAX+1)*(JMAX+1)-(2*J+1)*(J+1)*J/3)
      RETURN
    1 WLSTAT=REAL((JMAX+1)*(2*JMAX+3)*(2*JMAX+1)/3)
      RETURN
      END
      
C********************************************************************      
      FUNCTION WLLILI(E,JG,B1,B2)
C********************************************************************
C This function counts exact values of Wl(E,J) for systems linear/
C linear.
C********************************************************************
      WLLILI=0.
      J1MAX=INT(SQRT(.25+E/B1)-.5)
      DO 1 J1=0,J1MAX
      J2MAX=INT(SQRT(.25+(E-B1*J1*(J1+1))/B2)-.5)
      DO 1 J2=0,J2MAX
      DO 1 J=IABS(J1-J2),J1+J2
      DO 1 L=IABS(JG-J),JG+J
    1 WLLILI=WLLILI+1.
      RETURN
      END
      
C********************************************************************      
      FUNCTION WLLOLL(E,B1,B2)
C********************************************************************
C This function calculates classically  Wl(E,J=0) for systems linear/
C linear.
C********************************************************************
      WLLOLL=2.*E*SQRT(E)*(SQRT(B1)+SQRT(B2)-SQRT(B1+B2))/
     *(3.*B1*B2)
      RETURN
      END
      
C********************************************************************      
      FUNCTION WLHILL(E,B1,B2)
C*********************************************************************
C This function calculates classically Wl(E,highJ) for systems linear/
C linear.
C*********************************************************************
      WLHILL=E*E/(2.*B1*B2)
      RETURN
      END
      
C********************************************************************      
      FUNCTION WLSTLI(E,JG,BST,BLI)
C********************************************************************
C This function counts exact values of Wl(E,J) for systems spherical
C top/linear.
C********************************************************************
      WLSTLI=0.
      J1MAX=INT(SQRT(.25+E/BST)-.5)
      DO 1 J1=0,J1MAX
      J2MAX=INT(SQRT(.25+(E-BST*J1*(J1+1))/BLI)-.5)
      SWL=REAL(2*J1+1)
      DO 1 J2=0,J2MAX
      DO 1 J=IABS(J1-J2),J1+J2
      DO 1 L=IABS(JG-J),JG+J
    1 WLSTLI=WLSTLI+SWL
      RETURN
      END
      
C********************************************************************      
      FUNCTION WLLOSL(E,BST,BLI)
C**********************************************************************
C This function calculates classically Wl(E,J=0) for systems
C spherical top/linear.
C**********************************************************************
      WLLOSL=E*E*ASIN(SQRT(BST/(BST+BLI)))/(2.*BST*SQRT(BST)*SQRT(BLI))
      RETURN
      END
      
C********************************************************************      
      FUNCTION WLHISL(E,BST,BLI)
C**********************************************************************
C This function calculates classically Wl(E,highJ) for systems
C spherical top/linear.
C**********************************************************************
      WLHISL=8.*E*E*SQRT(E)/(15.*BST*SQRT(BST)*BLI)
      RETURN
      END
      
C********************************************************************      
      FUNCTION WLSTST(E,JG,BST1,BST2)
C**********************************************************************
C This function counts exact values of Wl(E,J) for systems spherical
C top/spherical top.
C**********************************************************************
      WLSTST=0.
      J1MAX=INT(SQRT(.25+E/BST1)-.5)
      DO 1 J1=0,J1MAX
      J2MAX=INT(SQRT(.25+(E-BST1*J1*(J1+1))/BST2)-.5)
      SWL1=REAL(2*J1+1)
      DO 1 J2=0,J2MAX
      SWL=REAL(2*J2+1)*SWL1
      DO 1 J=IABS(J1-J2),J1+J2
      DO 1 L=IABS(JG-J),JG+J
    1 WLSTST=WLSTST+SWL
      RETURN
      END
      
C********************************************************************      
      FUNCTION WLLOSS(E,BST1,BST2)
C**********************************************************************
C This function calculates classically Wl(E,J=0) for systems spherical
C top/spherical top.
C**********************************************************************
      WLLOSS=8.*E*E*SQRT(E)/(15.*BST1*BST2*SQRT(BST1+BST2))
      RETURN
      END
      
C********************************************************************      
      FUNCTION WLHISS(E,BST1,BST2)
C**********************************************************************
C This function calculates classically Wl(E,highJ) for systems
C spherical top/spherical top.
C**********************************************************************
      WLHISS=3.1416*E*E*E/(6.*BST1*BST2*SQRT(BST1*BST2))
      RETURN
      END
      
C********************************************************************      
      FUNCTION POLAT(X)
C**********************************************************************
C This function interpolates Wl(E,J) between Wl(E,J=0) and Wl(E,highJ)
C in a doubly reduced representation.
C**********************************************************************
      POLAT=TANH(X)*(.08*SQRT(X)*EXP(-1.1*(X-1.)*(X-1.))+1.)
      RETURN
      END
      
C********************************************************************      
      FUNCTION QSTAR(EPS,X,KT)
C*********************************************************************
C This function calculates (pseudo) partition functions for the
C "transition state", see JCP 75, 226 (1981), eqs. (6.4) and (6.6).
C*********************************************************************
      REAL KT
      DOUBLE PRECISION DGAMMA
      QSTAR=REAL(DGAMMA(DBLE(1.+X)))*(1.-EXP(-EPS/KT))**(-X)
      RETURN
      END
      
C********************************************************************      
      FUNCTION AWR(E,BETA)
C*********************************************************************
C This function calculates the Whitten-Rabinovitch a(E).
C*********************************************************************
      IF (E.GT.1.) THEN
                W=10**(-1.0506*E**.25)
          ELSE
                W=1/(5.*E+2.73*SQRT(E)+3.51)
      ENDIF
      AWR=1.-BETA*W
      RETURN
      END

